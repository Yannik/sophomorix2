#!/usr/bin/perl -w
# $Id$
# Dieses Script (sophomorix-class) wurde von Rüdiger Beck erstellt
# Es ist freie Software
# Bei Fehlern wenden Sie sich bitte an mich.
# jeffbeck@web.de  oder  jeffbeck@gmx.de


# ===========================================================================
# Bibliotheken
# ===========================================================================
use strict;
use Getopt::Long;
Getopt::Long::Configure ("bundling");
use Sophomorix::SophomorixConfig;
use Sophomorix::SophomorixBase;
use Sophomorix::SophomorixAPI;
use Sophomorix::SophomorixPgLdap;
use DBI;
use Net::LDAP;

my @arguments = @ARGV;

# ===========================================================================
# Optionen verarbeiten
# ==========================================================================
# Variablen für Optionen
$Conf::log_level=1;
my $help=0;
my $info=0;
my $class="";
my $hide="";
my $quota;
my $mailquota;
my $mailalias;
my $maillist;
$Conf::log_level=1;

# Parsen der Optionen
my $testopt=GetOptions(
           "help|h" => \$help,
           "info|i" => \$info,
           "class|c=s" => \$class,
           "hide!" => \$hide,
           "quota=s" => \$quota,
           "mailquota=i" => \$mailquota,
           "mailalias!" => \$mailalias,
           "maillist!" => \$maillist,
           "verbose|v+" => \$Conf::log_level,
          );

# Prüfen, ob Optionen erkannt wurden
&check_options($testopt);
&check_connections();
&log_script_start(@arguments);

if (not defined $quota 
    and not defined $mailquota
    and $info==0 
    and not defined $mailalias
    and not defined $maillist
    and $class eq ""){
    $help=1;
}


# --help
if ($help==1) {
   # Scriptname ermitteln
   my @list = split(/\//,$0);
   my $scriptname = pop @list;
   # Befehlbeschreibung
   print('
sophomorix-class sets properties of a class in the database

Options
  -h  / --help
  -v  / --verbose
  -vv / --verbose --verbose
  -i  / --info
  -c adminclass, / --class adminclass
    (can set values also for the group teachers with --class teachers)
  --hide --class adminclass, --nohide --class hiddenclass 
  --quota quotastring
  --mailquota number
  --mailalias, --nomailalias
  --maillist, --nomaillist
Please see the sophomorix-class(8) man pages for full documentation
');
   print "\n";
   exit;
}

############################################################
# config values
############################################################



# --info
if ($info==1){
   &show_class_list();
   exit;
}


# --mailalias
if (defined $mailalias){
    if ($class eq ""){
        print "   You must give a class to use --mailalias\n";
	exit;
    }
    if ($mailalias==0){
        &update_class_db_entry($class,"Mailalias=FALSE");
    } elsif ($mailalias==1){
        &update_class_db_entry($class,"Mailalias=TRUE");
    }
}


# --maillist
if (defined $maillist){
    if ($class eq ""){
        print "   You must give a class to use --maillist\n";
	exit;
    }
    if ($maillist==0){
        &update_class_db_entry($class,"Maillist=FALSE");
    } elsif ($maillist==1){
        &update_class_db_entry($class,"Maillist=TRUE");
    }
}


# --hide
if ($hide ne "" and $class ne ""){
    if ($hide==0){
        print "Unhiding class $class\n";
        &update_class_db_entry($class,"Type=adminclass");
    }
    if ($hide==1){
        print "Hiding class $class\n";
        &update_class_db_entry($class,"Type=hiddenclass");
    }
}





# --quota
if (defined $quota){
    if ($class eq ""){
        print "   You must give a class to use --quota\n";
	exit;
    }
    if ($quota eq "" or 
        $quota eq "quota" or 
        $quota eq "standard" or
	$quota eq "default"){
        &update_class_db_entry($class,"Quota=quota");
    } else {
        &update_class_db_entry($class,"Quota=$quota");
    }
}


# --mailquota
if (defined $mailquota){
    if ($class eq ""){
        print "   You must give a class to use --mailquota\n";
	exit;
    }
    &update_class_db_entry($class,"MailQuota=$mailquota");
}





# Setting Quota if necessary
if ($Conf::use_quota eq "yes" 
    and $class ne "" 
    and (defined $quota or defined $mailquota)
    ) {
    system("${DevelConf::executable_pfad}/sophomorix-quota --class $class  --noninteractive");
} else {
    &titel("NOT setting quota (nothing quota related changed)");
}





# Creating Mailing Aliases and Lists
if ($class ne ""
    and (defined $mailalias or defined $maillist)
   ) {
    system("${DevelConf::executable_pfad}/sophomorix-mail");
} else {
    &titel("NOT creating mailaliases/lists (nothing mail related changed)");
}


# ===========================================================================
# Ende des Scripts
# ===========================================================================
&log_script_end(@arguments);







